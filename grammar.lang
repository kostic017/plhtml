prog = '<' '!' TokDoctype TokHtml '>' '<' TokHTML TokLang '=' '"' TokIdentifier '"' '>' prog_header prog_body '<' '/' TokHTML '>';
prog_header = '<' TokHead '>' prog_title '<' '/' TokHEAD '>';
prog_title  = '<' TokTitle '>' TokStringConst '<' '/' TokTitle '>';
prog_body   = '<' TokBody '>' func_main '<' '/' TokBody '>';
func_main   = '<' TokMain '>' stmts '<' '/' TokMain '>';

stmt = stmt_ctrl
     | '<' TokVar TokClass '=' '"' TokIntType '"' '>' TokIdentifier '<' '/' TokVar '>'
     | '<' TokVar TokClass '=' '"' TokRealType '"' '>' TokIdentifier '<' '/' TokVar '>'
     | '<' TokVar TokClass '=' '"' TokBoolType '"' '>' TokIdentifier '<' '/' TokVar '>'
     | '<' TokVar TokClass '=' '"' TokStringType '"' '>' TokIdentifier '<' '/' TokVar '>'
     | '<' TokData TokValue '=' '"' expr '"' '>' TokIdentifier '<' '/' TokData '>'
     | '<' TokOutput '>' expr '<' '/' TokOutput '>'
     | '<' TokInput TokName '=' '"' TokIdentifier '"' '>'
     ;

stmt_ctrl = '<' TokDiv TokData '-' TokIf '=' '"' expr '"' '>' stmts '<' '/' TokDiv '>'
          | '<' TokDiv TokData '-' TokWhile '=' '"' expr '"' '>' stmts '<' '/' TokDiv '>'
          ;

; stmts = stmt*

stmts = stmt stmts
      |
      ;

expr_prim = TokIntConst
          | TokRealConst
          | TokBoolConst
          | TokStringConst
          | TokIdentifier
          | '(' expr ')'
          | '+' expr_prim
          | '-' expr_prim
          | '!' expr_prim
          ;

binop = '+'
      | '-'
      | '*'
      | '/'
      | TokLtOp
      | TokGtOp
      | TokLeqOp
      | TokGeqOp
      | TokEqOp
      | TokNeqOp
      ;

; expr = expr_prim (binop expr_prim)*;

expr = expr_prim binop_rhs;

binop_rhs = binop expr_prim binop_rhs
         |
         ;
